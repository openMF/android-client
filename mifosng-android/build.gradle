/*
 * This project is licensed under the open source MPL V2.
 * See https://github.com/openMF/android-client/blob/master/LICENSE.md
 */

buildscript {
    ext.kotlin_version = "1.8.20"
    repositories {
        google()
        mavenCentral()
        maven { url 'https://maven.fabric.io/public' }
        maven { url "https://jitpack.io" }
        maven { url "https://plugins.gradle.org/m2/" }
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:7.4.2'
        classpath 'io.fabric.tools:gradle:1.31.0'
        classpath 'com.github.triplet.gradle:play-publisher:1.1.5'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jetbrains.kotlin:kotlin-android-extensions:$kotlin_version"
        classpath "com.github.spotbugs.snom:spotbugs-gradle-plugin:5.0.14"
        classpath "androidx.navigation:navigation-safe-args-gradle-plugin:2.6.0"
        classpath 'com.google.dagger:hilt-android-gradle-plugin:2.44'
    }
}

repositories {
    google()
    mavenCentral()
    maven { url 'https://maven.fabric.io/public' }
    jcenter()
    maven { url "https://maven.google.com" }
    maven { url "https://jitpack.io" }
}

apply plugin: 'com.android.application'
apply plugin: 'io.fabric'
apply from: '../config/quality/quality.gradle'
apply plugin: 'com.github.triplet.play'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-parcelize'
apply plugin: 'androidx.navigation.safeargs.kotlin'
apply plugin: 'com.google.dagger.hilt.android'

android {
    compileSdkVersion rootProject.ext.compileSdkVersion

    defaultConfig {
        multiDexEnabled true
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode 6
        versionName "1.0.1"
        compileSdkPreview = "UpsideDownCake"
        // A test runner provided by https://code.google.com/p/android-test-kit/
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true

        ndk {
            abiFilters "armeabi-v7a", "x86", "x86_64", "arm64-v8a"
        }
    }

    dexOptions {
        javaMaxHeapSize "4g"
    }

    sourceSets {
        def commonTestDir = 'src/commonTest/java'
        main {
            java.srcDirs = ['src/main/java']
        }
        androidTest {
            java.srcDirs = ['src/instrumentTest/java/']
            java.srcDir commonTestDir
        }
        test {
            java.srcDir commonTestDir
        }
    }

    signingConfigs {
        release {
            storeFile file("../default_key_store.jks")
            storePassword "mifos1234"
            keyAlias "mifos"
            keyPassword "mifos1234"
        }
    }

    /*productFlavors {
    Keep it and the 'useProguard' attribute in the release config commented to prevent
    "Build-in class shrinker and multidex are not supported yet." error while enabling multidex
}*/

    buildTypes {

        debug {
            minifyEnabled false
            // TODO  Uses new built-in shrinker, To Enable update buils tools to 2.2
            // TODO http://tools.android.com/tech-docs/new-build-system/built-in-shrinker
            //useProguard false
            //proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            //testProguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguardTest-rules.pro'
        }

        release {
            minifyEnabled false
            //useProguard false
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            testProguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguardTest-rules.pro'
        }
    }
    lintOptions {
        abortOnError false
        disable 'InvalidPackage'
        disable 'MissingTranslation'
        disable 'OutdatedLibrary'
    }

    // Exclude duplicated Hamcrest LICENSE.txt from being packaged into the apk.
    // This is a workaround for https://code.google.com/p/android/issues/detail?id=65445.
    // The Hamcrest is used in tests.
    packagingOptions {
        exclude 'LICENSE.txt'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/dbflow-kotlinextensions-compileReleaseKotlin.kotlin_module'
    }
    useLibrary 'org.apache.http.legacy'

    // Always show the result of every unit test, even if it passes.
    testOptions.unitTests.all {
        testLogging {
            events 'passed', 'skipped', 'failed', 'standardOut', 'standardError'
        }
    }
    buildFeatures {
        viewBinding true
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_17.toString()
    }
}

task buildAndEMailAPK(type: Exec, description: 'Builds and Generates a Signed APK and emails it') {

}

tasks.withType(JavaCompile) {
    configure(options) {
        // TODO
        compilerArgs << "-Xlint:deprecation"
        compilerArgs << "-Xlint:-unchecked"
        compilerArgs << "-Xlint:-rawtypes"
    }
}

dependencies {
    // Multidex dependency
    implementation "androidx.multidex:multidex:$rootProject.multidexVersion"

    // Text drawable dependency
    implementation "com.github.amulyakhare:TextDrawable:$rootProject.amulyakhareVersion"

    // You must install or update the Support Repository through the SDK manager to use this dependency.
    implementation fileTree(dir: 'src/main/libs', include: ['*.jar'])

    // Kotlin standard library
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"

    //DBFlow dependencies
    kapt "com.github.raizlabs.dbflow.dbflow:dbflow-processor:$rootProject.raizLabsDBFlowVersion"
    implementation "com.github.raizlabs.dbflow.dbflow:dbflow:$rootProject.raizLabsDBFlowVersion"
    kapt "com.github.raizlabs.dbflow:dbflow-processor:$rootProject.raizLabsDBFlowProcessorVersion"

    // App's Support dependencies, including test
    implementation "androidx.appcompat:appcompat:$rootProject.supportLibraryVersion"
    implementation "androidx.legacy:legacy-support-v4:$rootProject.legacySupportVersion"
    implementation "androidx.recyclerview:recyclerview:$rootProject.recyclerViewVersion"
    implementation "com.google.android.material:material:$rootProject.designLibraryVersion"
    implementation "com.google.android.gms:play-services-places:$rootProject.playServicesPlacesVersion"
    implementation "com.google.android.gms:play-services-location:$rootProject.playServicesLocationsVersion"
    implementation "com.google.android.gms:play-services-maps:$rootProject.playServicesMapsVersion"
    implementation "com.google.maps.android:android-maps-utils:$rootProject.mapUtilsServicesVersion"
    implementation "androidx.test.espresso:espresso-idling-resource:$rootProject.espressoidlingVersion"

    //LifeCycle
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:$rootProject.lifecycleVersion"
    implementation "androidx.lifecycle:lifecycle-extensions:$rootProject.lifecycleExtensionsVersion"
    implementation "androidx.lifecycle:lifecycle-reactivestreams-ktx:$rootProject.lifecycleVersion"
    implementation "androidx.lifecycle:lifecycle-common-java8:$rootProject.lifecycleVersion"

    //Square dependencies
    implementation("com.squareup.retrofit2:retrofit:$rootProject.retrofitVersionLatest") {
        // exclude Retrofitâ€™s OkHttp peer-dependency module and define your own module import
        exclude module: 'okhttp'
    }
    implementation "com.squareup.retrofit2:converter-gson:$rootProject.retrofitVersionLatest"
    implementation "com.squareup.retrofit2:converter-scalars:$rootProject.retrofitVersionLatest"
    implementation "com.squareup.retrofit2:adapter-rxjava:$rootProject.retrofitVersionLatest"
    implementation "com.squareup.okhttp3:okhttp:$rootProject.okHttp3MainVersion"
    implementation "com.squareup.okhttp3:logging-interceptor:$rootProject.okHttp3Version"
    implementation "com.jakewharton.fliptables:fliptables:$rootProject.flipTableVersion"

    //sweet error dependency
    implementation "com.github.therajanmaurya:sweet-error:$rootProject.sweeterrorVersion"

    //rxjava dependencies
    implementation "io.reactivex:rxandroid:$rootProject.rxandroidVersion"
    implementation "io.reactivex:rxjava:$rootProject.rxjavaVersion"

    //stetho dependencies
    implementation "com.facebook.stetho:stetho:$rootProject.stethoVersion"
    implementation "com.facebook.stetho:stetho-okhttp3:$rootProject.stethoVersion"

    //showcase View dependency
    implementation "com.github.deano2390:MaterialShowcaseView:$rootProject.ShowcaseViewVersion"

    //Iconify dependency
    implementation "com.joanzapata.iconify:android-iconify-material:$rootProject.iconifyVersion"

    //crashlytics dependency
    implementation("com.crashlytics.sdk.android:crashlytics:$rootProject.crashlyticsVersion") {
        transitive = true
    }

    //glide dependency
    implementation "com.github.bumptech.glide:glide:$rootProject.glideVersion"

    //mifos passcode dependency
    implementation "com.mifos.mobile:mifos-passcode:$rootProject.mifosPasscodeVersion"

    // Android Testing Support Library's runner and rules dependencies
    androidTestImplementation "androidx.test:runner:$rootProject.androidTestRunnerVersion"
    androidTestImplementation "androidx.test:rules:$rootProject.androidTestRuleVersion"

    // Espresso UI Testing dependencies.
    androidTestImplementation "androidx.test.espresso:espresso-core:$rootProject.espressoVersion"
    androidTestImplementation("androidx.test.espresso:espresso-contrib:$rootProject.espressoVersion") {
        exclude group: 'com.android.support', module: 'appcompat'
        exclude group: 'com.android.support', module: 'support-v4'
        exclude group: 'com.android.support', module: 'recyclerview-v7'
        exclude group: 'com.android.support', module: 'design'
    }
    androidTestImplementation "androidx.test.espresso:espresso-intents:$rootProject.espressoVersion"

    // Mockito and jUnit dependencies
    testImplementation "junit:junit:$rootProject.jUnitVersion"
    testImplementation "org.mockito:mockito-core:$rootProject.mockitoVersion"
    androidTestImplementation "junit:junit:$rootProject.jUnitVersion"
    androidTestImplementation "org.mockito:mockito-core:$rootProject.mockitoVersion"
    androidTestImplementation "org.mockito:mockito-android:$rootProject.mockitoVersion"
    testImplementation "org.junit.jupiter:junit-jupiter:$rootProject.junitJupiterVersion"
    testImplementation "androidx.arch.core:core-testing:$rootProject.archCoreVersion"

    //Android-Jobs
    implementation "com.evernote:android-job:$rootProject.androidJobVersion"

    // androidx annotations
    implementation "androidx.annotation:annotation:$rootProject.annotationLibraryVersion"

    //preferences
    implementation "androidx.preference:preference-ktx:$rootProject.preferenceVersion"

    //Splash Screen
    implementation "androidx.core:core-splashscreen:$rootProject.splashscreenVersion"

    // Navigation Components
    implementation "androidx.navigation:navigation-fragment-ktx:$rootProject.navigationComponentsVersion"
    implementation "androidx.navigation:navigation-ui-ktx:$rootProject.navigationComponentsVersion"

    // Hilt dependency
    implementation "com.google.dagger:hilt-android:$rootProject.hiltVersion"
    kapt "com.google.dagger:hilt-android-compiler:$rootProject.hiltVersion"

    // fineract sdk dependencies
    implementation "com.github.openMF:mifos-android-sdk-arch:$rootProject.sdkVersion"

    // sdk client
    implementation "com.github.openMF:fineract-client:$rootProject.clientVersion"
}
/*
All direct/transitive dependencies shared between your test and production APKs need to be
excluded from the test APK! This is necessary because both APKs will contain the same classes. Not
excluding these dependencies from your test configuration will result in an dex pre-verifier error
at runtime. More info in this tools bug: (https://code.google.com/p/android/issues/detail?id=192497)
*/
configurations.implementation.dependencies.each { compileDependency ->
    println "Excluding compile dependency: ${compileDependency.getName()}"
    configurations.androidTestImplementation.dependencies.each { androidTestCompileDependency ->
        configurations.androidTestImplementation.exclude module: "${compileDependency.getName()}"
    }
}
